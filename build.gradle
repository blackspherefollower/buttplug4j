plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'checkstyle'
}

group = 'org.blackspherefollower.buttplug'
version = "3.0." + ("${System.env.GITHUB_RUN_NUMBER ?: 1}") + ("${System.env.GITHUB_REF_TYPE}" == "tag" ? "" : "-SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    api 'jakarta.websocket:jakarta.websocket-api:2.1.0'
    api 'org.eclipse.jetty.websocket:websocket-javax-client:10.0.13'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.eclipse.jetty.websocket:websocket-javax-server:10.0.14'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}
checkstyle {
    configFile = file("${rootDir}/checkstyle.xml")
    showViolations = false
    ignoreFailures = true
}
test {
    finalizedBy jacocoTestReport
    finalizedBy check
}
jacocoTestReport {
    dependsOn test
}
tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.env.GITHUB_REPOSITORY}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
