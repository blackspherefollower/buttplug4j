plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'checkstyle'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-annotations:2.19.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testImplementation 'dev.harrel:json-schema:1.8.2'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.compileJava {
    options.release.set(8)
}

checkstyle {
    configFile = file("${rootDir}/checkstyle.xml")
    showViolations = false
    ignoreFailures = true
}
test {
    finalizedBy jacocoTestReport
    finalizedBy check
}
jacocoTestReport {
    dependsOn test
}
tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.env.GITHUB_REPOSITORY}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Buttplug for Java'
                description = 'A pure Java Buttplug Client implementation'
                url = 'https://github.com/blackspherefollower/buttplug4j'
                inceptionYear = '2017'

                licenses {
                    license {
                        name = 'BSDv3 License'
                        url = 'https://github.com/blackspherefollower/buttplug4j/'
                    }
                }
                developers {
                    developer {
                        id = 'blackspherefollower'
                        name = 'BlackSphereFollower'
                        email = 'blackspherefollower@iostindex.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/blackspherefollower/buttplug4j.git'
                    developerConnection = 'scm:git:https://github.com/blackspherefollower/buttplug4j.git'
                    url = 'https://github.com/blackspherefollower/buttplug4j'
                }
            }
        }
    }
}

signing {
    def gpgKey = project.findProperty("gpg.key") ?: System.getenv("OSSRH_GPG") ?: ""
    def gpgPass = project.findProperty("gpg.pass") ?: System.getenv("OSSRH_GPG_PASS") ?: ""
    useInMemoryPgpKeys(gpgKey.replaceAll("\\\\n", "\n"), gpgPass)
    sign publishing.publications.mavenJava
}
